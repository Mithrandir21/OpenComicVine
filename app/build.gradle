plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.devtools.ksp'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {
    namespace 'org.proninyaroslav.opencomicvine'
    compileSdk 33

    defaultConfig {
        applicationId "org.proninyaroslav.opencomicvine"
        minSdk 21
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropertiesFile = rootProject.file('key.properties')
            if (keystorePropertiesFile.exists()) {
                keystorePropertiesFile.withReader('UTF-8') { reader ->
                    keystoreProperties.load(reader)
                }
            }

            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.3'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas".toString())
    arg("room.incremental", "true")
    arg("room.expandProjection", "true")
}

dependencies {
    def nav_version = '2.5.3'
    def room_version = '2.5.0'
    def retrofit_version = '2.9.0'
    def okhttp_version = '5.0.0-alpha.11'
    def moshi_version = '1.14.0'
    def moshi_sealed_version = '0.19.0'
    def accompanist_version = '0.28.0'
    def acra_version = '5.9.7'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.13.4'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    testImplementation 'io.kotest:kotest-assertions-core:5.5.5'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    // Debug
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // AndroidX Core
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // Android Material View
    implementation 'com.google.android.material:material:1.9.0-beta01'

    // Compose Core
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation 'androidx.compose.material3:material3:1.1.0-alpha08'
    implementation 'androidx.compose.material3:material3-window-size-class:1.1.0-alpha08'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.0'
    // Fallback import for components that aren't in Material 3
    implementation "androidx.compose.material:material:$compose_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // Navigation Compose
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'

    // Paging 3.0
    implementation 'androidx.paging:paging-compose:1.0.0-alpha18'
    implementation "androidx.room:room-paging:$room_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // Coil
    implementation 'io.coil-kt:coil-compose:2.2.2'

    // DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    // Accompanist
    implementation "com.google.accompanist:accompanist-placeholder-material:$accompanist_version"
    implementation "com.google.accompanist:accompanist-flowlayout:$accompanist_version"
    implementation "com.google.accompanist:accompanist-webview:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"

    // Moshi
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-adapters:$moshi_version"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    // moshi-sealed
    implementation "dev.zacsweers.moshix:moshi-sealed-runtime:$moshi_sealed_version"
    ksp "dev.zacsweers.moshix:moshi-sealed-codegen:$moshi_sealed_version"

    // ACRA
    implementation "ch.acra:acra-mail:$acra_version"
    implementation "ch.acra:acra-dialog:$acra_version"

    // Other
    implementation 'com.github.skydoves:sandwich:1.3.4'
    implementation 'com.github.SmartToolFactory:Compose-Image:1.2.1'
    implementation 'com.github.alorma:compose-settings-ui-m3:0.25.0'
}